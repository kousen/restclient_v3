plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
}

group = 'com.kousenit'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
    if (version.contains('-')) {
        maven { url "https://repo.spring.io/milestone" }
    }
    if (version.endsWith('-SNAPSHOT')) {
        maven { url "https://repo.spring.io/snapshot" }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // RestClient
    implementation 'org.springframework:spring-web:6.1.0-M2'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    boolean isMacOS = System.getProperty("os.name").toLowerCase().contains("mac")
    String architecture = System.getProperty("os.arch").toLowerCase()
    if (isMacOS && architecture == "aarch64") {
        implementation group: 'io.netty', name: 'netty-resolver-dns-native-macos',
                version: '4.1.85.Final', classifier: 'osx-aarch_64'
    }
}

tasks.named('test', Test) {
    useJUnitPlatform()
}
